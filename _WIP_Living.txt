alias dWallCooler d0
alias dGasSensor d1
alias dReturn d2
alias dAirVent d3
alias dWallHeater d4

alias rRoomTemp r0
alias rZero r1
alias rOne r2
alias rRoomPressure r4

define MINTEMP 293
define TEMPCUTOFF 298
define MAXTEMP 303
define MINPRESSURE 85
define MINPRESSURECUTOFF 87
define MAXPRESSURE 105
define MAXPRESSURECUTOFF 103

move rZero 0
move rOne 1

main:
yield
l rRoomTemp dGasSensor Temperature
bgt rRoomTemp MAXTEMP turnCoolerOn
blt rRoomTemp TEMPCUTOFF turnCoolerOff

heat:
blt rRoomTemp MINTEMP turnHeaterOn
bgt rRoomTemp TEMPCUTOFF turnHeaterOff

lowerpressure:
l rRoomPressure dGasSensor Pressure
bgt rRoomPressure MAXPRESSURE turnReturnOn
blt rRoomPressure MAXPRESSURECUTOFF turnReturnOff

raisepressure:
blt rRoomPressure MINPRESSURE turnAirOn
bgt rRoomPressure MINPRESSURECUTOFF turnAirOff

j main

turnCoolerOn:
s dWallCooler On rOne
j heat

turnHeaterOn:
s dWallHeater On rOne
j lowerpressure

turnCoolerOff:
s dWallCooler On rZero
j heat

turnHeaterOff:
s dWallHeater On rZero
j lowerpressure

turnReturnOff:
s dReturn On rZero
j raisepressure

turnReturnOn:
s dReturn On rOne
j raisepressure

turnAirOff:
s dAirVent On rZero
j main

turnAirOn:
s dAirVent On rOne
j main
